JSPlot interop.
JSPlot is served over HTTP as a portable web interface. It requests data via
AJAX, and may request the same data multiple times to save browser memory. The
JSPlot driver buffers the data to disk to make it repeatable.

use constant jsplot_gen => gen $self{'core/jsplot/html'};

use constant jsplot_html =>
  jsplot_gen->(css => $self{'core/jsplot/css'},
               js  => join '', @self{qw| core/jsplot/jquery.min.js
                                         core/jsplot/jquery.mousewheel.min.js
                                         core/jsplot/jsplot.js |});

JSPlot data streaming.
This is the websocket connection that ni uses to stream data to the client. Any
data we receive from the client indicates that the client is canceling the
websocket request, so we need to break the pipe and kill off subprocesses.

sub jsplot_stream($$@) {
  local $_;
  my ($reply, $req, @ni_args) = @_;
  my ($ops) = cli @ni_args;
  unless (defined $ops) {
    my (undef, @rest) = parse pcli_debug '', @ni_args;
    die "ni: jsplot failed to parse starting at @rest";
  }

  printf STDERR "\nni --js: running %s\n", json_encode $ops;

  safewrite $reply, ws_header($req);
  my $ni_pipe = sni @$ops, http_websocket_encode_batch_op 65536;

  my $incoming;
  my $rmask   = '';
  vec($rmask, fileno $reply, 1) = 1;

  my $n;
  my $total = 0;
  while ($n = saferead $ni_pipe, $_, 65536) {
    printf STDERR "\rni --js: % 8dKiB", ($total += $n) >> 10;
    if (select my $rout = $rmask, undef, undef, 0) {
      saferead $reply, $incoming, 8192;
      if ($incoming =~ /^\x81\x80/) {
        print STDERR "\nni --js: closed worker pipe\n";
        return;
      }
    }
    safewrite $reply, $_;
  }

  print STDERR "\nni --js: done\n";
}

sub jsplot_server {
  my ($port) = @_;
  load 'core/http/ws.pm';
  http $port, sub {
    my ($url, $req, $reply) = @_;
    return print "http://localhost:$port/\n"             unless defined $reply;
    return http_reply $reply, 200, jsplot_html           if $url eq '/';
    return jsplot_stream($reply, $req, shell_unquote $1) if $url =~ /^\/ni\/(.*)/;
    return http_reply $reply, 404, $url;
  };
}

defclispecial '--js', q{jsplot_server $_[0] || 8090};
