#!/usr/bin/env perl
# Verifies that the given series of commands, when executed, produces the
# specified output.

use Cwd;
use File::Temp qw/tmpnam/;

our $original = getcwd;
our $tmpdir = tmpnam;
mkdir $tmpdir;

print STDERR "testing in $tmpdir\n";

sub verify {
  my ($c, $o) = @_;
  return unless defined $c;
  print STDERR "\$ $c";

  chdir $tmpdir or die "failed to chdir $tmpdir: $!";
  my $result = qx/$c/;
  $result eq $o
    or die "expected '$c' to produce '$o', but got '$result' instead";
  chdir $original;
}

my $command = undef;
my $output  = undef;
while (<>) {
  if (s/^\$//) {
    verify $command, $output;
    $command = $_;
    $command .= ($_ = <>) while /\\$/;
    $output  = '';
  } else {
    $output .= $_;
  }
}

verify $command, $output;

system 'rm', '-r', $tmpdir;
