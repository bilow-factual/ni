#!/usr/bin/env perl
# Verifies that the given series of commands, when executed, produces the
# specified output.

use Cwd;
use File::Temp qw/tmpnam/;

our $original = getcwd;
our $tmpdir = tmpnam;
mkdir $tmpdir;

our $commands = 0;
our $failures = 0;

print STDERR "testing in $tmpdir\n";

sub verify {
  my ($c, $o) = @_;
  return unless defined $c;
  print STDERR "\$ $c";

  chdir $tmpdir or die "failed to chdir $tmpdir: $!";
  my $q = $c =~ s/'/'\\''/gr;
  my $result = qx/sh -c '$q'/;

  ++$failures,
  print STDERR "expected '$c' to produce (\n$o\n), but got (\n$result\n) instead\n"
    unless $result eq $o;

  chdir $original;
}

my $command  = undef;
my $output   = undef;
while (<>) {
  if (s/^\$\s+//) {
    ++$commands;
    verify $command, $output;
    $command = $_;

    my $quoted = length s/[^']//gr;
    while ($quoted & 1 || /\\$/) {
      $command .= ($_ = <>);
      $quoted += length s/[^']//gr;
    }

    $output = '';
  } else {
    $output .= $_;
  }
}

verify $command, $output;

system 'rm', '-r', $tmpdir;

print "\n\n$commands command(s) total\n";
print "$failures failed command(s)\n";

exit !!$failures;
